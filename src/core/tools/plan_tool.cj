package cli.core.tools

import cli.utils.*
import cli.core.config.CliConfig

import magic.dsl.*
import magic.prelude.*
import magic.log.LogUtils

import std.collection.*
import std.fs.*

private class Task {
    let description: String
    var completed: Bool

    public init(description: String) {
        this.description = description
        this.completed = false
    }

    public func markAsComplete(): Unit {
        this.completed = true
    }

    public func toMarkdown(): String {
        if (this.completed) {
            return "- [x] ${this.description}"
        } else {
            return "- [ ] ${this.description}"
        }
    }

    public static func fromMarkdown(line: String): Task {
        // Line must start with "- [ ] " or "- [x] "
        let task = Task(line[5..].trimAscii())
        if (line.startsWith("- [x]")) {
            task.markAsComplete()
        }
        return task
    }
}

private class Plan <: ToString {
    let tasks: ArrayList<Task>

    init() {
        this.tasks = ArrayList<Task>()
    }

    init(tasks: Array<String>) {
        this.tasks = ArrayList<Task>(tasks.size, { i => Task(tasks[i]) })
    }

    public func isEmpty(): Bool {
        return this.tasks.isEmpty()
    }

    override public func toString(): String {
        return String.join(
            this.tasks |> map { task => task.toMarkdown() } |> collectArray,
            delimiter: "\n"
        )
    }

    public func save(): Unit {
        let path = CliConfig.todoFile
        try {
            let file = File(path, Write)
            let content = this.toString()
            file.write(content.toArray())
            file.close()
        } catch(e: Exception) {
            LogUtils.error("é”™è¯¯ï¼šæ— æ³•ä¿å­˜è®¡åˆ’æ–‡ä»¶åˆ° ${path}ã€‚ è¯¦æƒ…: ${e}")
        }
    }

    public static func fromFile(): Plan {
        let plan = Plan()
        let content = try {
            String.fromUtf8(File.readFrom(CliConfig.todoFile))
        } catch (e: FSException) {
            LogUtils.debug("æ–‡ä»¶æˆ–ç›®å½•ä¸å­˜åœ¨æˆ–æ— æ³•è®¿é—®")
            return plan
        }
        for (line in content.split("\n")) {
            if (!line.trimAscii().isEmpty()) {
                plan.tasks.add(Task.fromMarkdown(line))
            }
        }
        return plan
    }
}

@toolset
public class PlanTool {
    private var plan = Plan.fromFile()

    @tool[
        description: "åˆ›å»ºä¸€ä¸ªæ–°çš„ä»»åŠ¡è®¡åˆ’ï¼Œæˆ–ç”¨æ–°è®¡åˆ’è¦†ç›–ç°æœ‰è®¡åˆ’ã€‚è¿™æ˜¯å¼€å§‹å¤æ‚ä»»åŠ¡çš„ç¬¬ä¸€æ­¥ã€‚",
        parameters: { tasks: "ä¸€ä¸ªå­—ç¬¦ä¸²æ•°ç»„ï¼Œæ¯é¡¹éƒ½æ˜¯ä¸€ä¸ªæ¸…æ™°çš„å­ä»»åŠ¡æè¿°" }
    ]
    public func createOrUpdatePlan(tasks: Array<String>): String {
        this.plan = Plan(tasks)
        let message = "\n${this.plan}\nè®¡åˆ’å·²åˆ›å»ºï¼Œè¯·ç¡®è®¤æ˜¯å¦éœ€è¦ä¿®æ”¹è®¡åˆ’ï¼Œç¡®è®¤æ‰§è¡Œåˆ™ä¿å­˜è¯¥è®¡åˆ’ï¼Œå¹¶å¼€å§‹æ‰§è¡Œã€‚"
        if (let Deny(userInput) <- InputUtils.confirm("Plan", message)) {
            return "ç”¨æˆ·å–æ¶ˆæ‰§è¡Œè¯¥æ“ä½œï¼Œç”¨æˆ·æ„è§åé¦ˆï¼š${userInput}"
        }
        this.plan.save()
        return "\nè®¡åˆ’å·²è·å¾—ç”¨æˆ·æ‰¹å‡†ï¼Œå‡†å¤‡å¼€å§‹æ‰§è¡Œã€‚"
    }

    @tool[
        description: "æŸ¥çœ‹å½“å‰çš„è®¡åˆ’å’Œæ¯ä¸ªä»»åŠ¡çš„å®ŒæˆçŠ¶æ€ (TODO/DONE)ã€‚"
    ]
    public func viewPlan(): String {
        if (this.plan.isEmpty()) {
            return "å½“å‰æ²¡æœ‰è®¡åˆ’ã€‚"
        }
        return "å½“å‰è®¡åˆ’:\n${this.plan}"
    }

    @tool[
        description: "å°†æŒ‡å®šç´¢å¼•çš„ä»»åŠ¡æ ‡è®°ä¸ºå·²å®Œæˆ (DONE)ï¼Œå¹¶è¿”å›å½“å‰çš„ TODO List",
        parameters: { taskIndex: "è¦æ ‡è®°ä¸ºå®Œæˆçš„ä»»åŠ¡çš„ç´¢å¼• (ä»1å¼€å§‹)" }
    ]
    public func markTaskAsComplete(taskIndex: Int64): String {
        if (taskIndex < 1 || taskIndex > this.plan.tasks.size) {
            return "é”™è¯¯: æ— æ•ˆçš„ä»»åŠ¡ç´¢å¼• ${taskIndex}ã€‚"
        } else if (this.plan.tasks[taskIndex].completed) {
            return "ä»»åŠ¡ ${taskIndex} å·²ç»æ˜¯å®ŒæˆçŠ¶æ€ã€‚"
        }
        this.plan.tasks[taskIndex-1].markAsComplete()
        this.plan.save()
        PrintUtils.printTool("ğŸ“ Current Plan", this.plan.toString())
        return "ä»»åŠ¡ ${taskIndex} å·²è¢«å®Œæˆï¼Œå½“å‰ä»»åŠ¡åˆ—è¡¨ï¼š\n${this.plan}"
    }
}